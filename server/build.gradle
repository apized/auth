plugins {
  id 'org.apized.micronaut' version "$apizedVersion"
  id 'jacoco'
}

dependencies {
  annotationProcessor 'io.micronaut.openapi:micronaut-openapi'

  implementation "org.apized:micronaut-metrics:$apizedVersion"
  implementation "org.apized:micronaut-tracing:$apizedVersion"
  implementation "org.apized:micronaut-messaging-rabbitmq:$apizedVersion"

  implementation 'com.auth0:java-jwt:4.3.0'
}

application {
  mainClass.set("org.apized.auth.Application")
}


dockerBuild {
  images.set(["${System.getenv('DOCKER_REGISTRY') ? "${System.getenv('DOCKER_REGISTRY')}/apized/auth-server" : parent.name}:jvm-${System.getenv("IMAGE_TAG") ?: version}"])
}

dockerBuildNative {
  images.set(["${System.getenv('DOCKER_REGISTRY') ? "${System.getenv('DOCKER_REGISTRY')}/apized/auth-server" : parent.name}:${System.getenv("ARCH") ?: "x86"}-${System.getenv("IMAGE_TAG") ?: version}"])
}

dockerfile {
  baseImage("alpine:20230901")
  runCommand('apk --no-cache add openjdk21')
}

dockerfileNative {
  setGraalImage("ghcr.io/graalvm/native-image-community:21")
  baseImage('alpine:3.17')
  runCommand('apk add gcompat libstdc++')
}

test {
  if (project.hasProperty('excludeTests')) {
    project.properties['excludeTests']?.replaceAll('\\s', '')?.split('[,;]')?.each {
      exclude "${it}"
    }
  }
}
